# -*- mode: shell-script -*-
# vim: set fdm=marker ft=zsh :

PSUSER="%(#.%F{red}.%F{green})%n%f"
PSSEP='@'
PSHOST="%F{yellow}%m%f"
PSDIR="%(#.%F{magenta}.%F{cyan})%B%(4~:../%3~:%~)%b%f"

PS1='\
${PRIV+%F{red\}%B"PRIV"%b%f }\
$PSUSER\
$PSSEP\
$PSHOST\
\
${AMOUNT_DEVICE:+ %F{green\}%B$AMOUNT_DEVICE%b%f}\
 $PSDIR\
${ZLOCAL:+ $ZLOCAL}\
%(#. %F{red}%BΩ%b%f. %F{blue}%Bλ%b%f) '

RPS1='%(?..%F{red}%B$?%b%f )${vcs_info_msg_0_:+$vcs_info_msg_0_}'

# git stuff
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:*' check-for-changes true
# %u if there are unstaged changes
zstyle ':vcs_info:*' unstagedstr   "%F{red}"
# %c if there are staged changes
zstyle ':vcs_info:*' stagedstr     "%F{green}"
                                   #<branch><changes><unstaged>
zstyle ':vcs_info:*' formats       "%F{black}%B%s:%%b%f%F{blue}%B%c%u%b%%b%f"
zstyle ':vcs_info:*' actionformats "%F{black}%B%s:%%b%f%F{blue}%B%c%u%b%%b%f %F{black}%K{yellow}(%a)%%b%f"


autoload -U add-zsh-hook

# for git annex direct repos
vcs_info_silent()
{
    vcs_info &> /dev/null
}

add-zsh-hook precmd vcs_info_silent

precmd-bell()
{
    # bell on prompt display BUT not when just starting the terminal
    [ "$HAS_STARTED" = 1 ] && echo -n "\a" || HAS_STARTED=1
}
add-zsh-hook precmd precmd-bell
