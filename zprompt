# -*- mode: shell-script -*-
# vim: set fdm=marker ft=zsh :

PSUSER="%(#.%F{red}.%F{green})%n%f"
PSSEP='@'
if [ -n "$SSH_CONNECTION" ]; then
    PSHOST="%F{yellow}%B%m%b%f"
else
    PSHOST="%F{yellow}%m%f"
fi
PSDIR="%(#.%F{magenta}.%F{cyan})%B%(4~:../%3~:%~)%b%f"

PS1='\
${PSINFO:+$PSINFO%b%f }\
${PRIV+%F{red\}%B"PRIV"%b%f }\
$PSUSER\
$PSSEP\
$PSHOST \
\
${AMOUNT_DEVICE:+%F{green\}%B$AMOUNT_DEVICE%b%f }\
$PSDIR \
${LOCAL_HISTFILE:+%F{magenta\}#%f }\
${ZLOCAL:+$ZLOCAL }\
%(#.%F{red}%BΩ%b%f.%F{blue}%Bλ%b%f) '

PS2='%_ %F{blue}%B>>=%b%f '

RPS1='%(?..%F{red}%B$?%b%f )$vcs_info_msg_0_${ZTODO_PROMPT:+ %F{black\}%B|%b%f$ZTODO_PROMPT}'

# git stuff
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-unapplied true
zstyle ':vcs_info:*' use-quilt true
# %u if there are unstaged changes
zstyle ':vcs_info:*' unstagedstr   "%b%F{red}"
# %c if there are staged changes
zstyle ':vcs_info:*' stagedstr     "%F{green}"
                                   #<branch><changes><unstaged>
zstyle ':vcs_info:*' formats       "%F{black}%B%s:%%b%f%F{blue}%B%c%u%b%%b%f%Q"
zstyle ':vcs_info:*' actionformats "%F{black}%B%s:%%b%f%F{blue}%B%c%u%b%%b%f%Q %F{black}%K{yellow}(%a)%%b%f"
zstyle ':vcs_info:*' patch-format   ":%p%F{magenta}[%n/%a]%f"
zstyle ':vcs_info:*' nopatch-format ":%F{cyan}%B[0/%a]%b%f"


autoload -U add-zsh-hook

# for git annex direct repos
vcs_info_silent()
{
    vcs_info &> /dev/null
}

add-zsh-hook precmd vcs_info_silent

precmd-bell()
{
    # bell on prompt display BUT not when just starting the terminal
    [ "$HAS_STARTED" = 1 ] && echo -n "\a" || HAS_STARTED=1
}
add-zsh-hook precmd precmd-bell
