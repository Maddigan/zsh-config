# -*- mode: shell-script -*-
# vim: set fdm=marker ft=zsh :

PSUSERCOLOR=$RED
PSUSER="%n"
PSSEP1='@'
PSHOSTCOLOR=$GREEN
PSHOST="%m"

jobcount()
{
    if [ -n "$(jobs)" ]; then
        echo "───(${YELLOW}J$RESET)"
    fi
}

TTYNAME=`tty | sed "s#/[^/]*/##"`

if [ -n "$AMOUNT_DEVICE" ]; then
    AMOUNT_DEVICE_MARKER="───($GREEN$BOLD"$AMOUNT_DEVICE"$RESET)"
fi

if [ -n "$PRIV" ]; then
    PRIV_MARKER="───($RED$BOLD"PRIV"$RESET)"
fi

PS1='\
┌───\
%{($BOLD$TTYNAME$RESET)%}\
$SSH_MARKER\
$AMOUNT_DEVICE_MARKER\
$(jobcount)\
$PRIV_MARKER\
───\
(%{$BLUE$BOLD%}%~%{$RESET%})\

└(\
\
%{$PSUSERCOLOR%}\
$PSUSER\
%{$RESET%}\
$PSSEP1\
%{$PSHOSTCOLOR%}\
$PSHOST\
%{$RESET%}\
$PSSEP2\
$PSDIR\
%# '

RPS1='%(?..[%{$RED$BOLD%}$?%{$RESET%}])'

# git stuff
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
# %u if there are unstaged changes
zstyle ':vcs_info:*' unstagedstr   "%{$RED%}"
# %c if there are staged changes
zstyle ':vcs_info:*' stagedstr     "%{$GREEN%}"
                                   #<branch><changes><unstaged>
zstyle ':vcs_info:*' formats       "%{$BLUE$BOLD%}%c%u(%b)%{$RESET%}"
zstyle ':vcs_info:*' actionformats "%{$BLUE$BOLD%}%c%u(%b)%{$RESET%} %{$BLACK$ON_YELLOW%}(%a)%{$RESET%}"

PERIOD=15

precmd()
{
    vcs_info
    if [ -n "$vcs_info_msg_0_" ]; then
        PSSEP2=":"
        PSDIR="$vcs_info_msg_0_"
    else
        PSSEP2=
        PSDIR=
    fi
}

command -v ssh_prompt &> /dev/null && ssh_prompt
