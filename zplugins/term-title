# -*- mode: shell-script -*-
# vim: ft=zsh fdm=marker :

if [ "$TERM" = "linux" ]; then
    return 0;
fi

# copied from grml with some modifications

function set_title () {
    info_print  $'\e]0;' $'\a' "$@"
}
function info_print () {
    local esc_begin esc_end
    esc_begin="$1"
    esc_end="$2"
    shift 2
    printf '%s' ${esc_begin}
    printf '%s' "$*"
    printf '%s' "${esc_end}"
}
function grml_control_xterm_title () {
    case $TERM in
        (xterm*|rxvt*)
            set_title "${(%):-"${RANGER_LEVEL:+(ranger) }%m: %~:"}" "$1"
            ;;
    esac
}
function grml_maintain_name () {
    # set hostname if not running on host with name 'grml'
    if [[ -n "$HOSTNAME" ]] && [[ "$HOSTNAME" != $(hostname) ]] ; then
       NAME="@$HOSTNAME"
    fi
}
function grml_reset_screen_title () {
    # adjust title of xterm
    # see http://www.faqs.org/docs/Linux-mini/Xterm-Title.html
    [[ ${NOTITLE:-} -gt 0 ]] && return 0
    case $TERM in
        (xterm*|rxvt*)
            set_title ${(%):-"${RANGER_LEVEL:+(ranger) }%m: %~"}
            ;;
    esac
}

autoload -U add-zsh-hook
add-zsh-hook precmd grml_reset_screen_title
add-zsh-hook preexec grml_maintain_name
add-zsh-hook preexec grml_control_xterm_title
